unit uProfessor;

interface

uses
  System.Classes, System.SysUtils, System.Generics.Collections;

type
  // Classe modelo para Professor
  TProfessor = class
  private
    FCodigo: Integer;
    FNome: string;
    FCPF: string;
  public
    constructor Create(aCodigo: Integer; aNome, aCPF: string);
    property Codigo: Integer read FCodigo write FCodigo;
    property Nome: string read FNome write FNome;
    property CPF: string read FCPF write FCPF;
    function ToString: string; override;
  end;

  // Controlador para gerenciar Professores
  TProfessorControlador = class
  private
    FLista: TObjectList<TProfessor>;
    FArquivo: string;
    function BuscarIndice(aCodigo: Integer): Integer;
    function ObterProximoCodigo: Integer;
    function ExisteNome(const aNome: string): Boolean;
    function ExisteCPF(const aCPF: string): Boolean;
  public
    constructor Create;
    destructor Destroy; override;

    function Incluir(aCodigo: Integer; const aNome, aCPF: string): Boolean;
    function Atualizar(aCodigo: Integer; const aNome, aCPF: string): Boolean;
    function Excluir(aCodigo: Integer): Boolean;
    function BuscarPorCodigo(aCodigo: Integer): TProfessor;
    procedure Listar(AStringList: TStrings);
    procedure CarregarDados;
    procedure SalvarDados;
  end;

implementation

// --------------------------------------------------------------------------------------------------
// TProfessor
// --------------------------------------------------------------------------------------------------

constructor TProfessor.Create(aCodigo: Integer; aNome, aCPF: string);
begin
  inherited Create;
  FCodigo := aCodigo;
  FNome := aNome;
  FCPF := aCPF;
end;

function TProfessor.ToString: string;
begin
  Result := Format('Código: %d - Nome: %s - CPF: %s', [FCodigo, FNome, FCPF]);
end;

// --------------------------------------------------------------------------------------------------
// TProfessorControlador
// --------------------------------------------------------------------------------------------------

constructor TProfessorControlador.Create;
begin
  inherited Create;
  FLista := TObjectList<TProfessor>.Create(True);
  FArquivo := 'professores.txt';
end;

destructor TProfessorControlador.Destroy;
begin
  FLista.Free;
  inherited Destroy;
end;

function TProfessorControlador.BuscarIndice(aCodigo: Integer): Integer;
var
  i: Integer;
begin
  Result := -1;
  for i := 0 to FLista.Count - 1 do
  begin
    if FLista[i].Codigo = aCodigo then
    begin
      Result := i;
      Break;
    end;
  end;
end;

function TProfessorControlador.ObterProximoCodigo: Integer;
var
  i: Integer;
  maiorCodigo: Integer;
begin
  maiorCodigo := 0;
  for i := 0 to FLista.Count - 1 do
  begin
    if FLista[i].Codigo > maiorCodigo then
      maiorCodigo := FLista[i].Codigo;
  end;
  Result := maiorCodigo + 1;
end;

function TProfessorControlador.ExisteNome(const aNome: string): Boolean;
var
  i: Integer;
begin
  Result := False;
  for i := 0 to FLista.Count - 1 do
  begin
    if SameText(FLista[i].Nome, aNome) then
    begin
      Result := True;
      Break;
    end;
  end;
end;

function TProfessorControlador.ExisteCPF(const aCPF: string): Boolean;
var
  i: Integer;
begin
  Result := False;
  for i := 0 to FLista.Count - 1 do
  begin
    if FLista[i].CPF = aCPF then
    begin
      Result := True;
      Break;
    end;
  end;
end;

function TProfessorControlador.Incluir(aCodigo: Integer; const aNome, aCPF: string): Boolean;
begin
  Result := False;
  if BuscarIndice(aCodigo) <> -1 then
    raise Exception.Create('Código de professor já existe');
  if Trim(aNome) = '' then
    raise Exception.Create('Nome do professor não pode estar vazio');
  if Trim(aCPF) = '' then
    raise Exception.Create('CPF do professor não pode estar vazio');
  
  if ExisteNome(aNome) then
    raise Exception.Create('Já existe um professor com este nome');
  if ExisteCPF(aCPF) then
    raise Exception.Create('Já existe um professor com este CPF');

  try
    FLista.Add(TProfessor.Create(aCodigo, aNome, aCPF));
    SalvarDados;
    Result := True;
  except
    on E: Exception do
      raise Exception.Create('Erro ao incluir professor: ' + E.Message);
  end;
end;

function TProfessorControlador.Atualizar(aCodigo: Integer; const aNome, aCPF: string): Boolean;
var
  indice: Integer;
  i: Integer;
begin
  Result := False;
  indice := BuscarIndice(aCodigo);

  if indice < 0 then
    raise Exception.Create('Professor não encontrado');
  if Trim(aNome) = '' then
    raise Exception.Create('Nome do professor não pode estar vazio');
  if Trim(aCPF) = '' then
    raise Exception.Create('CPF do professor não pode estar vazio');

  for i := 0 to FLista.Count - 1 do
  begin
    if (i <> indice) and SameText(FLista[i].Nome, aNome) then
      raise Exception.Create('Já existe outro professor com este nome');
  end;
  for i := 0 to FLista.Count - 1 do
  begin
    if (i <> indice) and (FLista[i].CPF = aCPF) then
      raise Exception.Create('Já existe outro professor com este CPF');
  end;
  
  try
    FLista[indice].Nome := aNome;
    FLista[indice].CPF := aCPF;
    SalvarDados;
    Result := True;
  except
    on E: Exception do
      raise Exception.Create('Erro ao atualizar professor: ' + E.Message);
  end;
end;

function TProfessorControlador.Excluir(aCodigo: Integer): Boolean;
var
  indice: Integer;
begin
  Result := False;
  indice := BuscarIndice(aCodigo);
  if indice >= 0 then
  begin
    try
      FLista.Delete(indice);
      SalvarDados;
      Result := True;
    except
      on E: Exception do
        raise Exception.Create('Erro ao excluir professor: ' + E.Message);
    end;
  end;
end;

function TProfessorControlador.BuscarPorCodigo(aCodigo: Integer): TProfessor;
var
  indice: Integer;
begin
  Result := nil;
  indice := BuscarIndice(aCodigo);
  if indice >= 0 then
    Result := FLista[indice];
end;

procedure TProfessorControlador.Listar(AStringList: TStrings);
var
  professor: TProfessor;
begin
  AStringList.Clear;
  for professor in FLista do
    AStringList.Add(professor.ToString);
end;

procedure TProfessorControlador.CarregarDados;
var
  F: TFileStream;
  professor: TProfessor;
  i, codigo: Integer;
  tamanhoNome, tamanhoCPF: Integer;
  nome, cpf: string;
begin
  if not FileExists(FArquivo) then Exit;
  F := TFileStream.Create(FArquivo, fmOpenRead);
  try
    FLista.Clear;
    while F.Position < F.Size do
    begin
      F.Read(codigo, SizeOf(Integer));
      F.Read(tamanhoNome, SizeOf(Integer));
      SetLength(nome, tamanhoNome);
      F.Read(nome[1], tamanhoNome);
      F.Read(tamanhoCPF, SizeOf(Integer));
      SetLength(cpf, tamanhoCPF);
      F.Read(cpf[1], tamanhoCPF);
      professor := TProfessor.Create(codigo, nome, cpf);
      FLista.Add(professor);
    end;
  finally
    F.Free;
  end;
end;

procedure TProfessorControlador.SalvarDados;
var
  F: TFileStream;
  professor: TProfessor;
  tamanhoNome, tamanhoCPF: Integer;
begin
  F := TFileStream.Create(FArquivo, fmCreate);
  try
    for professor in FLista do
    begin
      F.Write(professor.Codigo, SizeOf(Integer));
      tamanhoNome := Length(professor.Nome);
      F.Write(tamanhoNome, SizeOf(Integer));
      F.Write(professor.Nome[1], tamanhoNome);
      tamanhoCPF := Length(professor.CPF);
      F.Write(tamanhoCPF, SizeOf(Integer));
      F.Write(professor.CPF[1], tamanhoCPF);
    end;
  finally
    F.Free;
  end;
end;

end.